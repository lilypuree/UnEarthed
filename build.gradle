plugins {
	id 'fabric-loom' version '0.6.9'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '4.0.4'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://maven.fabricmc.net/io/github/prospector/modmenu/' }
	maven {
		name "cotton-config"
		url 'http://server.bbkr.space:8081/artifactory/libs-release'
	}
}

loom {
	accessWidener "src/main/resources/unearthed.aw"
}

configurations {
	shade
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	include modImplementation ("me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	include modImplementation ("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	include modImplementation ("io.github.prospector:modmenu:${project.mod_menu_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}

	include modImplementation ("tk.valoeghese:ZoesteriaConfig:1.3.2") {
		exclude group: "net.fabricmc.fabric-api"
	}

	shade implementation("net.openhft:zero-allocation-hashing:0.11")

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

shadowJar {
	classifier = ''

	configurations = [project.configurations.shade]

	relocate 'net.openhft', "${project.group}.shadow.net.openhft"
}

remapJar {
	dependsOn shadowJar

	input = shadowJar.archivePath

	classifier = 'all'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}


jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}
